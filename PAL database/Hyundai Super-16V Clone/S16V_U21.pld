Name     S16V_U21 ;
PartNo    ;
Date     15.03.2024 ;
Revision 01 ;
Designer Dmitry Shtatnov ;
Company  SSSR Labs ;
Assembly None ;
Location U21;
Device   g16v8ma ;

/* WARNING!
   THIS FILE DOESN'T COMPILE LINE 2048 WITH XOR SETTINGS!
   ALL OUTPUT POLARITY MUST BE SET TO INVERTED (0)!
   THE OUTPUT JED FILE MUST CONTAIN THE FOLLOWING LINE:
   *L02048 00000000001100000011000000100000
*/   

/* Inputs */
PIN 1 = A1;
PIN 2 = A2;
PIN 3 = A3;
PIN 4 = A4;
PIN 5 = A5;
PIN 6 = A6;
PIN 7 = A7;
PIN 8 = A8;
PIN 9 = A9;
PIN 11 = AEN;
PIN 13 = !IOR;
PIN 14 = !DACK2;
PIN 15 = !ROM0;
PIN 18 = !ROM1;

/* Outputs */
PIN 12 = !EPROM_OE;
PIN 16 = !LDOR;
PIN 17 = !FDD_CS;
PIN 19 = !FDD_DATA_READ;

/* Equations */
                /*
                111111x10x: 3f4, 3f5, 3fc, 3fd
                3F4: FDD controller status register,
                3F5: FDD data register (write 1-9 byte command, see INT 13)
                3FC: UART MCR - Modem Control Register
                3FD: UART LSR - Line Status Register

                111111x01x: 3f2, 3f3, 3fa, 3fb
                3F2: FDD controller control port (16 bit)
                3FA: UART IIR - Interrupt Identification Register (read) /
                3FA: UART FCR - 16550 FIFO Control Register (write)
                3FB: LCR - Line Control Register

                11x1111xxx: 370..377, 3f0..3f7
                Floppy disk controller 0, 1
                
                1x010xxxxx: 240..25f, 340..35f
                240-25F: RTC?
                340-35F:  Reserved for I/O channel
                */
FDD_DATA_READ = (IOR & !AEN & A9 & A8 & A7 & A6 & A5 & A4 & A2 & !A1) #
                (IOR & !AEN & A9 & A8 & A7 & A6 & A5 & A4 & !A2 & A1) #
                (IOR & !AEN & A9 & A8 & A6 & A5 & A4 & A3) #
                (IOR & !AEN & A9 & !A7 & A6 & !A5) #
                (DACK2 & IOR);

                /*
                111111010x: 3f4, 3f5
                3F4: FDD controller status register,
                3F5: FDD data register (write 1-9 byte command, see INT 13)
                */
FDD_CS =        (!AEN & A9 & A8 & A7 & A6 & A5 & A4 & !A3 & A2 & !A1);

                /*
                111111001x: 3f2, 3f3
                3F2: FDD controller control port (16 bit)
                */
LDOR =          (!AEN & A9 & A8 & A7 & A6 & A5 & A4 & !A3 & !A2 & A1);

EPROM_OE =      (ROM0) # (ROM1);
